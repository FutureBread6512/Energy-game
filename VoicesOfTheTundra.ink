VAR player_name = "Александр"
VAR companion = "Вика"
VAR has_data = false
VAR told_about_arylhan = false
VAR arylhan_met_again = false
VAR осмотрел_дома = false
VAR поговорил_со_старушкой = false

=== come_in ===
#Location: come_in
2030 год, Окраина арктической, русской земли. Меня и еще троих моих однокурсников с факультета энергетики отправили в экспедицию к морю Лаптевых.

Вика, Анна, Максим, я и наш Научный руководитель. Сказали выполнить какое-то стажерское задание.

Вика - прагматичный технарь и карьеристка, она видит в экспедиции шанс для прорыва и продвижения инновационных энергетических решений.

Анна - эколог-идеалист, глубоко переживающая за хрупкую арктическую природу.

Максим - осторожный реалист и дипломат, выполняет задания с минимальными рисками.
Научный руководитель: Возьмите. Прочитайте, пока в дороге.
* Читать
    -> task

=== task ===
#Location: task
* Вернуться к наблюдению
    -> Home

=== Home ===
#Location: Home
Мы остановились жить в небольшом домике на окраине, в поселке Баай. 

Научный руководитель: Как вы уже знаете, вы должны определиться с источником энергии для этого региона, используя собранные вами и до вас данные.

Научный руководитель: Пока не стемнело, вы должны собрать данные с местной метеорологической станции и разобрать вещи.

Анна: Я останусь в лагере.

Александр: Я пойду на станцию.

Научный руководитель: Вика, Максим?

Они промолчали.

Научный руководитель: Понятно. Александр, кого возьмешь с собой?
* Вика
    ~ companion = "Вика"
    Научный руководитель: Отлично! Отправляйтесь. Вернитесь до заката.
    -> outside
* Максим
    ~ companion = "Максим"
    Научный руководитель: Отлично! Отправляйтесь. Вернитесь до заката.
    -> outside
    
=== outside ===
#Location: outside
Мы направились к станции. Август холодный.

{companion == "Вика": 
    Вика шла быстрым шагом, почти не глядя по сторонам.
    
    Вика: Чем быстрее мы соберем данные, тем скорее сможем приступить к анализу. Тем более, скоро ночь. Быстро похолодает
    Александр: Ты так уверена, что мы найдем здесь перспективный источник энергии?
    Вика: Приливной потенциал моря Лаптевых теоретически один из самых высоких в Арктике. Если данные подтвердятся, мы сможем предложить установку подводных турбин. Это был бы прорыв.
    Александр: А как же экологические риски? Анна говорит...
    Вика: *перебивая* Анна идеалистка. Прогресс требует компромиссов. Главное - правильно рассчитать нагрузки и минимизировать воздействие. Но без энергии этот поселок так и останется проблемной зоной перебоев электроснабжения
    Она посмотрела на темнеющее небо.
    
    Вика: Хотя... кажется, погода портится. Надо ускориться.
}
- else:
    Максим шел неторопливо, внимательно осматривая местность.
    
    Максим: Интересное место. Суровое, но красивое.
    Александр: Думаешь, наши исследования могут навредить местной среде?
    Максим: Любое вмешательство человека в природу имеет последствия.
    Александр: Вика считает, что нужно действовать быстро и решительно.
    Максим: Вика видит только техническую сторону. Но здесь живут люди, есть своя культура, уклад. И природа, которая не прощает ошибок. Нужно тщательно взвесить все варианты.
    
    Он застегнул куртку плотнее.
    
    Максим: Похоже, будет буря. Может, вернемся?
    Александр: Нам нужно собрать данные.
    Максим: Как скажешь. Но будь готов к тому, что придется пережидать.
-> meteo_near

=== meteo_near ===
#Location: meteo_near
Когда мы подобрались к станции, буря уже была сильной.

Александр: Мы сделали это, {companion}!
Александр: {companion}?

Никого не было. Из-за бури пропала видимость.

Александр: Где же {companion}? Надеюсь, {companion} в деревне, а не на вьюге.

Я кричу: {companion}!. В ответ - завывание ветра.

Надо собрать данные, пока я здесь.

~ has_data = true
-> meteo_storm

=== meteo_storm ===
#Location: meteo_storm
Данные собраны
Буря свистит, но на станции безопасно.

Начинает темнеть, пора возвращаться.

???: ЭЙ! ЕСТЬ КТО ЖИВОЙ?

Это с улицы. Кто в такую непогоду снаружи?

???: Тот, кто на метеостанции, можешь впустить меня?
* Да, заходи
    -> welcome_in
* Промолчать
    -> not_welcome

=== welcome_in ===
Дверь отворилась. На пороге мужчина в шубе, ушанке и с голубыми, как лед, глазами.
-> meteo

=== not_welcome ===
Дверь отворилась. На пороге мужчина в шубе, ушанке и с голубыми, как лед, глазами.

???: Я ж знал, что ты здесь. Почему не отвечаешь?

Александр: ...
* Промолчать
    ???: Чего молчишь? Я тебя наверное напугал, да?
    -> meteo
* Что выдало меня?
    ???: Свет горит
    -> meteo

=== meteo ===
#Location: meteo
Арылхан: Эх, спасибо, что пустил. Меня зовут Арылхан, я местный, из Баай. Буря скоро закончится. Но лучше переночевать здесь. Ночью по улице не надо ходить. Благо есть все для этого.

Мы сели за стол, где работники обычно обедают.

Арылхан: Расскажешь о себе? Откуда и зачем приехал? Я же вижу, ты не местный.
* Рассказать
    -> tell_about
* Я не буду рассказывать незнакомцу о себе
    -> not_tell

=== tell_about ===
Арылхан: Так значит вы сюда приехали за энергией? Никто уже не думает о природной ценности региона, его особенностях
Александр: О чем вы?
Арылхан: Наш закуток отрезан от единой сети электроснабжения, как и большая часть территории тут. Какие-нибудь аналитики сказали бы, что показатель экологии один из лучших - нет загрязнений.
Арылхан: Но эта величина сегодня обратна пропорциональна счастью населения. Одно предпретие на город - это ведь скучно. Не думай, что это только из-за отуствия современных благ здесь. Эта проблема существовала всегда, с момента появления человека.
* Неужели нет способа найти баланс?
Арылхан: Это разве не твоя работа? Найти наиболее рациональное решение для поддержания комфортной жизни людей и функционала окружающей среды?
 Александр: Да, все так. Но я пока не уверен, какое решение.. правильное?
Арылхан: Понял. Ты не один - вот и примете решение вместе.
-> sleep

=== not_tell ===
Арылхан: Ну как знаешь.
-> sleep

=== sleep ===
Арылхан: Утро вечера мудренее. Спокойной ночи.
Мы легли на диваны. Сон быстро взял надо мной вверх...
-> Village_after_storm

=== Village_after_storm ===
#Location: Village_after_storm
Утро. Бури как будто и не было. Тишина.

Александр: Нужно возвращаться.

Арылхан уже ушел.

По дороге не покидали мысли о нем.
Кто это? Почему оказался именно в том месте? 
Его высказывания о проблемы "Человек и природа".. Это ведь правда
Суровая бьющая по морали правда, затавяющая переосмыслить свое место в экосистеме жизни.
Следует ли людям быть здесь? Может, оставить эту землю для распоряжения ветрам и снегам?
* Закончить миссию - уйти с экспедиции
    -> Ending_three
* Необходимо продолжить - закончим начатое
    -> campus

=== campus ===
#Location: campus
Александр: *Стучит* Откройте, пожалуйста!

Дверь открыл Научный руководитель.

Научный руководитель: Не может быть! Ты живой! Заходи скорее. Сейчас заварим чай, тебе надо отогреться.
* Войти
    -> Home_return

=== Home_return ===
#Location: Home
Научный руководитель: Мы потеряли тебя.

Анна: Когда приш{ companion == "Вика" ? "ла" : "ел" } {companion}, { companion == "Вика" ? "она" : "он" } рассказал{ companion == "Вика" ? "а" : "" }, что случилось. Что тебя мгновенно накрыла буря и тебя не было видно.

{companion}: Я пытался тебя найти, но ветер сбивал с ног. Пришлось бежать обратно. Прости.

Александр: Со мной все в порядке. Я переночевал на станции.

Научный руководитель: За стол!

-> Tea

=== Tea ===
#Location: Tea
Научный руководитель: Тебе удалось забрать данные?
* Да, вот они
    -> data_taken

=== data_taken ===
Научный руководитель: Отлично! Как пережил ночь? Не замерз?
* Рассказать об Арылхане
    ~ told_about_arylhan = true
    -> tell_about_Arylhan
* Рассказать, что был один
    -> tell_alone

=== tell_alone ===
Александр: Я был один. Переждал бурю на станции.

Научный руководитель: Хорошо, что ты в порядке. Но все же, будь осторожен.

Вика: Давайте продолжим исследования, пока не стемнело.

Максим: Я предлагаю разделиться. Я и Анна пройдем по маршруту вдоль побережья, замерим потенциал ветра и приливов.

Научный руководитель: Хорошо. Я и Вика проанализируем данные о солнечной активности. А ты, Александр, останешься в деревне. Знаю, вчерашняя буря тебя не остановит. Иди в деревню: изучи, как тут устроен быт. Как местные решают энергетический вопрос? Есть ли генераторы, дизели? Может, уже экспериментируют с чем-то? Это важные фоновые данные.

Александр:
* Да, вы правы! Изучу деревню.
    -> village_day

=== tell_about_Arylhan ===
Александр: Местный. Арылхан. Говорил, что ночью по улице ходить нельзя. Помог переждать бурю.

Научный руководитель: Арылхан? *Научный руководитель тяжело вздыхает.* Опять он. Не слушайте его. Он тут у всех на виду. Местный, живет на окраине. Говорит, что с нефтью придет смерть, а с огнем от духов леса - процветание. Безумец, но безобидный. Держитесь от него подальше.

Вика: Не думаю, что мы его встретим вновь. Давайте продолжим исследования, пока не стемнело.

Максим: Я предлагаю разделиться. Я и Анна пройдем по маршруту вдоль побережья, замерим потенциал ветра и приливов.

Научный руководитель: Хорошо. Я и Вика проанализируем данные о солнечной активности. А ты, Александр, останешься в деревне. Знаю, вчерашняя буря тебя не остановит твой исследовательский дух. Иди в деревню: изучи, как тут устроен быт. Как местные решают энергетический вопрос? Есть ли генераторы, дизели? Может, уже экспериментируют с чем-то?

Александр:
* Да, вы правы! Изучу деревню.
    -> village_day

=== village_day ===
#Location: outside
День ясный и холодный. Посёлок «Баай» состоит из нескольких улиц и домов. Одни похожи на крепкие, приземистые избы, другие же - мощные панельки-стены.

{осмотрел_дома && поговорил_со_старушкой:
    -> after_questions
}

{not осмотрел_дома:
    + [Осмотреть ближайшие дома]
        -> looking
}

{not поговорил_со_старушкой:
    + Найти кого-то похожего на старожила
        -> old_lady
}

=== looking ===
#Location: looking
Вы подходите к дому. На крыше – не привычные антенны, а аккуратные солнечные панели, но прикрытые сверху слоем прозрачного, прочного гидрофобного материала. Сбоку, на шесте, тихо вращается компактная вертикальная ветротурбина. Окна – толстые, с вакуумными стеклопакетами. Из трубы идет слабый, почти незаметный пар. 
Этот дом привлек ваше внимание, так как обладает наибольшим количеством разных генераторов на возобновляемой энергии. На других домах похожие сооружения. Где-то есть похожее комбо, где-то элементы.

~ осмотрел_дома = true
-> village_day

=== old_lady ===
#Location: old_lady
Возле самого большого дома вы встречаете пожилую женщину, которая чинит сеть для рыбалки.

Александр: Здравствуйте. Можно спросить?

Женщина: Спрашивай, приезжий. Вижу, тебя не напугала вчерашняя буря, раз вышел на улицу снова.

+ Спросить про электричество
    -> ask_electricity
+ Спросить про Арылхана
    -> ask_Arylhan

-> village_day

=== ask_electricity ===
Александр: У вас тут очень современно. Солнце, ветер.

Женщина: Ага, пилотная программа. Хорошее это дело, но полученной энергии хватает только на свое хозяйство. Летом от солнца, зимой от ветра. Умно сделано.
Но основное снабжение все еще идет от станции. Все думают закрыть. Кто-то говорит, в этих местах могут находиться газ или нефть, как под соседними поселениями. И тогда-то это производство будет работать над местным топливом!

-> old_lady

=== ask_Arylhan ===
Александр: Знаете, кто такой Арылхан?

Женщина: *Вздыхает.* Уже успел встретить? Умный парень был, инженер-самоучка. А потом... что-то случилось. Книг своих начитался. Говорит, что техника – это хорошо, но дух Севера надо чувствовать. 
Женщина: Никто не знает, где он живет. Кто-то даже считает, что он сам является частью этого места. А так часто на метеостанции пропадает, наблюдает за чем-то своим.
-> old_lady

=== after_questions ===
#Location: outside
+ Пройти к окраине, где стоят генераторы
    -> generators

=== generators ===
#Location: generators
Вы решили пройти к окраине, где возвышается какое-то сооружение. Выходите к ограждению. Это дизельная электростанция. Рядом – ангар со множеством аккумуляторных блоков в утепленных контейнерах.
В нескольких метрах стоит главный хозяйственный комплекс - рыбохранилище. А напротив - рыбоконсервный завод. Можно считать, что это градообразующее предприятие.
А за строением.. Арылхан? Что он тут делает? Он стоит перед входом, словно ждет того, кто сейчас выйдет со смены.
* позвать
    ~ arylhan_met_again = true
    -> call_Arylhan
* пройти мимо
    -> pass_by

=== call_Arylhan ===
#Location: generators

Александр: Арылхан!

Арылхан медленно оборачивается. Его голубые глаза, как и в прошлый раз, кажутся кусками арктического льда. Он не выглядит удивленным.

Арылхан: Я знал, что мы встретимся снова. Ты вернулся в деревню изучать местный быт?

Александр: Да, нужно понять, как вы здесь живете. Что это за ангар с батареями?

Арылхан: Хранилище энергии. Неэффективное и дорогое. 

Александр: Но это же прогресс! Люди получают энергию.

Арылхан: Прогресс? Сюда принесли дизель, потом панели и ветряки. Каждая установка несет вместе с собой не только энергию - но и разрушение. А теперь твоя группа хочет поставить турбины в море, верно?

Александр: Откуда ты знаешь?

Арылхан: Я вижу это по твоим глазам. По тому, как ты смотришь на море. Вы все одинаковые: приезжаете, смотрите на цифры, на графики, но не видите главного.

Александр: (Мысли) А что главное?

* Спросить про духов
    -> what_spirit
* Спросить про разрушение от станций
    -> what_destroing
* Спросить, почему он против прогресса
    -> what_wrong_progress

=== what_spirit ===
Александр: Вы считаете, что духи саботируют местное производство?

Арылхан: Энергия - это жизнь. Дизель сжигает древнюю жизнь, похороненную под землей. Духи - истинные хозяева здесь. Таково устройство дел здесь

Александр: Но для жизни людей тоже нужна энергия!

Арылхан: Нужна, но как ее используют? Бесконтрольно включат каждый экран или используют для приготовления пищи?

-> choose

=== what_destroing ===
Александр: Какое разрушение ты имеешь в виду?

Арылхан: Тебе стыдно не знать, молодой ученый. Каждую станцию на возобновляемой энергии ставят туда, где можно будет наибольшую выработку. Но такая установка требует изменение ландшафта. 
Арылхан: Ветровая - шум, отпугивающие все живое; Гидро - затопление территорий; Солнечная - изменение поверхностного слоя земли, над которым оно установлено. На другие типы тоже можешь продолжить.
Арылхан: Ты можешь возразить, что это только для крупных сооружений. Ну а меньший масштаб не обеспечить всю деревню
-> choose

=== what_wrong_progress ===
Александр: Почему ты так против прогресса? Ведь новые технологии могут улучшить жизнь!

Арылхан: Посмотри. *Он указывает на рыбоконсервный завод.* Построили завод, чтобы ловить больше рыбы. Теперь рыбы становится меньше. Поставили дизельный генератор, чтобы было больше энергии. Теперь зимой ее все равно не хватает, ведь сюда сложно добраться. 

Александр: Но есть и хорошие примеры! Ветер и солнце - чистые источники!

Арылхан: Чистые для вас. Для природы - это все равно вмешательство. Вопрос только в масштабе. Нефть убивает быстро и заметно. Ветряки и панели - медленно и незаметно. Но итог один: разрыв связи.
-> choose

=== choose ===
Арылхан смотрит на вас внимательно, как бы оценивая.

Арылхан: Я вижу, ты не похож на других. В тебе есть сомнение.
* Согласиться с его точкой зрения
    -> arylhan_right
* Остаться при своем мнении
    -> arylhan_neutral
* Предложить компромисс
    -> arylhan_compromise

=== arylhan_right ===
Александр: Возможно, ты прав. Мы действительно многое не учитываем.

Арылхан: Редкий гость способен это признать. Запомни: когда будешь принимать решение, спроси себя - что скажет об этом ветер? Что почувствует земля?
-> bye

=== arylhan_neutral ===
Александр: Я понимаю твою точку зрения, но люди не могут вернуться в прошлое. Нужно искать современные решения.

Арылхан: Ищешь "решения", но не видишь, что проблема в самом вопросе. Ну что ж, каждый идет своим путем.
-> bye

=== arylhan_compromise ===
Александр: Может быть, можно найти баланс? Использовать технологии, но с уважением к природе?

Арылхан: Баланс... Интересное слово. Но баланс возможен только между равными, как в математике. Реальный мир не является математической моделью, где все идеально. Не думаю, что он возможен. Только приближение
-> bye

=== bye ===
Арылхан: Заходи на чай, если хочешь. Ты найдешь меня у метеостанции. Но я знаю, у тебя дела.

Он поворачивается к заводу, его фигура кажется частью этого сурового пейзажа.

Александр: Спасибо за разговор.

Арылхан: *Не оборачиваясь* Помни о выборе, который сделаешь. Он определит не только судьбу этого места.

-> evening_return

=== pass_by ===
Вы решаете не подходить и идете дальше.
-> evening_return

=== evening_return ===
Александр: Вечереет. Пора вернуться.
-> campus_evening

=== campus_evening ===
#Location: campus_evening
Вечер. Все собрались в доме. Научный руководитель готовит ужин на газовой турстической плите

Научный руководитель: Рассказывайте, что узнали?

Вика: Данные впечатляют! Приливной потенциал у побережья феноменальный. Можно ставить подводные турбины. Ветровые потоки стабильны и сильны. Я уже составила предварительный план гибридной системы: ветер + приливы + резервные солнечные панели для полярного дня.

Максим: Технически реализуемо, но нужно учитывать ледовую нагрузку и влияние на береговую линию. Также не обойдется без изменения местного рельефа.

Анна: Приливные электростанции нарушат жизнь всего прибрежного сообщества – от планктона до птиц. Ветряки – опасность для перелетных путей.
Анна: Солнечная энергетика, данные которой мы сегодня обработали, является перспективной для развития. Но сами генераторы.. Любая буря - и надо их счищать.

Вика: Может, у кого-то есть другие предложения? Думаю, очевидно, что нас послали сюда для написания плана по внедрению станций на возобновляемых источниках энергии. Местную станцию обслуживать дорого, так она еще и на дизеле. 

Максим: Александр, есть предложения? Ты сегодня увидел то, какова обывательская жизнь.
Александр: Я предлагаю обсудить все варианты
    -> discuss

=== discuss ===
Александр: Мы понимаем, что нет универсального варианта. Необходимо перед окончательным решением просчитать все риски. Все ваши идеи имеют право на существование. Максим и Вика, передайте мне информацию об энергии воды; Анна - солнечную. На основе выводов я приму самый рациональный вариант.
-> summary

=== summary ===
#Location: summary

Какова судьба региона?
* Станции на угле и газе - рядом добывают, получаем тепло и электроэнергию
    -> Ending_one
* Станции на возобновляемых источниках - дешевое производство и доступная электроэнергия
    -> Ending_two
* Лучше уйти отсюда
    -> Ending_three

=== Ending_one ===
#Location: Ending_one
Спустя годы ледовой щит стал держаться меньше. Средняя температура увеличивалась. Рыба ушла от берегов, нерпы тоже. Арылхана больше никто не видел.
-> END

=== Ending_two ===
#Location: Ending_two
Спустя года ледовой щит стал держаться чуть меньше. Рыба ушла от берегов, нерпы тоже. Птицы почти все улетели. Арылхан стоял на мысе и следил за техническим прогрессом.
-> END

=== Ending_three ===
#Location: Ending_three
Вы решили уйти. Проект был закрыт. Посёлок продолжал жить своей жизнью, но прогресс не пришел.
-> END

